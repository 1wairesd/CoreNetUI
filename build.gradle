plugins {
    id 'maven-publish'
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.wairesd.discordbm'
version = '1.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        mavenCentral()
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(17)
    }

    if (project.name in ['DiscordBMB', 'DiscordBMV']) {
        tasks.shadowJar {
            archiveClassifier.set('')
            mergeServiceFiles()
        }

        tasks.build {
            dependsOn(tasks.shadowJar)
        }

        processResources {
            def props = [version: version]
            inputs.properties props
            filteringCharset 'UTF-8'
            filesMatching(['plugin.yml', 'velocity-plugin.json']) {
                expand props
            }
        }
    }
}
