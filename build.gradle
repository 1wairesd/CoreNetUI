plugins {
    id 'maven-publish'
    id 'java'
    id 'xyz.jpenilla.run-velocity' version '2.3.1'
    id 'xyz.jpenilla.run-paper'    version '2.3.1'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.wairesd.discordbm'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url  = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url  = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'io.netty:netty-all:4.2.0.Final'
    compileOnly "org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT"
    compileOnly("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
}

tasks {
    runServer {
        minecraftVersion("1.20.4")
    }

    shadowJar {
        archiveClassifier.set('')
        relocate 'net.dv8tion.jda', 'com.wairesd.shaded.jda'
        relocate 'org.sqlite', 'com.wairesd.shaded.sqlite'
        mergeServiceFiles()
    }

    build {
        dependsOn(tasks.shadowJar)
    }
}

def targetJavaVersion = 17

java {
    sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
    targetCompatibility = JavaVersion.toVersion(targetJavaVersion)
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
