plugins {
    id 'maven-publish'
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-rc1'
}

def loadSecrets() {
    def secretsFile = file('maven.secret')
    if (secretsFile.exists()) {
        Properties props = new Properties()
        props.load(secretsFile.newDataInputStream())
        return props
    }
    return new Properties()
}

def secrets = loadSecrets()

group = 'com.wairesd.discordbm'
version = properties['version']

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    repositories {
        mavenCentral()
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        maven { url 'https://oss.sonatype.org/content/groups/public/' }
    }

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
        options.release.set(17)
    }

    if (project.name in ['bukkit', 'velocity']) {
        tasks.named('shadowJar') {
            archiveClassifier.set('')
            mergeServiceFiles()
        }

        tasks.named('build') {
            dependsOn(tasks.named('shadowJar'))
        }

        processResources {
            def props = [version: version]
            inputs.properties props
            filteringCharset 'UTF-8'
            filesMatching(['plugin.yml', 'velocity-plugin.json']) {
                expand props
            }
        }
    }
}
