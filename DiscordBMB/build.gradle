plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-paper' version '2.3.1'
    id 'maven-publish'
}

group = 'com.wairesd.discordbm.bukkit'
version = '1.0'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId = 'com.wairesd.discordbm'
            artifactId = 'DiscordBMB'
            version = '1.0'
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.extendedclip.com/repository/placeholderapi/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':common:client-common')
    implementation project(':common')
    implementation project(':api')

    implementation("org.yaml:snakeyaml:2.4")
    implementation("net.kyori:adventure-api:4.21.0")
    implementation("org.spongepowered:configurate-core:4.1.2")
    implementation("org.spongepowered:configurate-yaml:4.1.2")
    implementation("org.slf4j:slf4j-api:2.0.17")
    implementation("io.netty:netty-all:4.2.1.Final")

    compileOnly("com.github.placeholderapi:placeholderapi:2.11.6")
    compileOnly("org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT")
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-Xlint:deprecation")
    options.encoding = 'UTF-8'
    options.release.set(17)
}

tasks.named('compileJava') {
    dependsOn(':common:build')
}

tasks.named('shadowJar') {
    dependsOn(':common:build', ':api:build')
    archiveClassifier.set('')
    mergeServiceFiles()
    from(project(':common').sourceSets.main.output)
}

tasks.named('build') {
    dependsOn(tasks.named('shadowJar'))
}

tasks.named('runServer') {
    minecraftVersion("1.20.4")
}

tasks.named('processResources') {
    def props = [version: version]
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand(props)
    }
}
