plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'xyz.jpenilla.run-velocity' version '2.3.1'
}

group = 'com.wairesd.discordbm.velocity'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    implementation project(':DiscordBMB')
    implementation project(':common')

    implementation("org.apache.commons:commons-lang3:3.17.0")
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'org.json:json:20240303'
    implementation 'net.sf.trove4j:trove4j:3.0.3'
    implementation 'com.neovisionaries:nv-websocket-client:2.14'
    implementation 'org.yaml:snakeyaml:2.4'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.15.3'
    implementation 'org.apache.commons:commons-collections4:4.4'

    implementation 'org.xerial:sqlite-jdbc:3.49.1.0'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'io.netty:netty-all:4.2.0.Final'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
    annotationProcessor("com.velocitypowered:velocity-api:3.4.0-SNAPSHOT")
}

tasks {
    runVelocity {
        velocityVersion("3.4.0-SNAPSHOT")
    }

    shadowJar {
        archiveClassifier.set('')
        relocate 'net.dv8tion.jda', 'com.wairesd.shaded.jda'
        relocate 'okhttp3', 'com.wairesd.shaded.okhttp3'
        relocate 'gnu.trove', 'com.wairesd.shaded.gnu.trove'
        relocate 'org.json', 'com.wairesd.shaded.org.json'
        relocate 'com.neovisionaries', 'com.wairesd.shaded.com.neovisionaries'
        relocate 'com.fasterxml.jackson', 'com.wairesd.shaded.jackson'
        relocate 'org.apache.commons.collections4', 'com.wairesd.shaded.org.apache.commons.collections4'

        from project(':common').sourceSets.main.output
        mergeServiceFiles()
    }

    build {
        dependsOn(tasks.shadowJar)
    }

    compileJava {
        dependsOn ':DiscordBMB:build'
    }
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(17)
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('velocity-plugin.json') {
        expand props
    }
}

import org.gradle.api.file.DuplicatesStrategy

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}