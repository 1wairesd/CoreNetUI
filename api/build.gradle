plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'com.wairesd.discordbm'
version = properties['api']

repositories {
    mavenCentral()
}

dependencies {
    compileOnly("org.jetbrains:annotations:26.0.2")
    compileOnly("org.projectlombok:lombok:1.18.32")

    compileOnly("net.kyori:event-method:3.0.0")
    compileOnly("com.google.guava:guava:33.4.8-jre")
    compileOnly("org.checkerframework:checker-qual:3.42.0")

    annotationProcessor("org.projectlombok:lombok:1.18.32")

    testImplementation("org.junit.jupiter:junit-jupiter-api:5.13.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.13.1")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'api'

            from components.java

            pom {
                name = 'DiscordBM API'
                description = 'API for DiscordBM plugin'
                url = 'https://github.com/wairesd/DiscordBM'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id = 'wairesd'
                        name = 'wairesd'
                        email = 'wairesd@example.com'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/wairesd/DiscordBM.git'
                    developerConnection = 'scm:git:git@github.com:wairesd/DiscordBM.git'
                    url = 'https://github.com/wairesd/DiscordBM'
                }
            }
        }
    }
}